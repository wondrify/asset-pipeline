apply plugin: 'groovy'
apply plugin: 'java-library'

group = 'cloud.wondrify'

ext {
    pomDescription = 'Provides LESS Compiling support for the Asset-Pipeline library.'
}

configurations {
    provided
}
 
sourceSets {
    main {
        compileClasspath += configurations.provided
    }
}

dependencies {

    api project(':asset-pipeline-core')
    api "org.mozilla:rhino:$rhinoVersion"
    api "com.github.sommeri:less4j:$less4jVersion"
    api 'com.google.protobuf:protobuf-java:3.21.12'
    implementation "com.github.sommeri:less4j-javascript:$less4jJsVersion", {
        // An old forked version of Rhino is included in less4j-javascript
        exclude group: 'ro.isdc.wro4j', module: 'rhino'
    }

    // Needs to be compiled with Groovy 3 as it is used by the Gradle Plugin
    compileOnly "org.codehaus.groovy:groovy:$groovy3Version"
    compileOnly "org.slf4j:slf4j-api:$slf4jVersion"
}

task(console, dependsOn: 'classes', type: JavaExec) {
  main = 'groovy.ui.Console'
  classpath = sourceSets.main.runtimeClasspath
}

apply {
    from rootProject.layout.projectDirectory.file('gradle/java-config.gradle')
    from rootProject.layout.projectDirectory.file('gradle/publish-config.gradle')
}


