buildscript {
    repositories {
        // mavenLocal()
        mavenCentral()
        maven {
            url "https://xbib.org/repository"
        }
    }
    dependencies {
        classpath 'org.xbib.gradle.plugin:gradle-plugin-jflex:1.1.0'
    }
}

apply plugin: 'groovy'
apply plugin: 'java-library'
apply plugin: 'org.xbib.gradle.plugin.jflex'

group = 'cloud.wondrify'

sourceCompatibility = '1.17'
targetCompatibility = '1.17'

ext {
    pomDescription = 'JSX Transpiler Extension for the JVM Based Asset-Pipeline.'
}

repositories {
    mavenLocal()
    mavenCentral()
}

configurations {
    provided
}
sourceSets {
    main {
        compileClasspath += configurations.provided
    }
}

java {
    withSourcesJar()
    withJavadocJar()
}

dependencies {

    api project(':asset-pipeline-core')

    // Needs to be compiled with Groovy 3 as it is used by the Gradle Plugin
    compileOnly "org.codehaus.groovy:groovy:$groovy3Version"
    compileOnly "org.graalvm.polyglot:polyglot:$graalvmVersion"

    testImplementation "org.apache.groovy:groovy:$groovy4Version"
    testImplementation "org.graalvm.js:js-community:$graalvmVersion"
    testImplementation "org.spockframework:spock-core:$spockVersion"
}

task(console, dependsOn: 'classes', type: JavaExec) {
  main = 'groovy.ui.Console'
  classpath = sourceSets.main.runtimeClasspath
}

test {
    useJUnitPlatform()
    testLogging {
        events('passed', 'skipped', 'failed')
        exceptionFormat = 'full'
        showStandardStreams = true
    }
}

sourcesJar {
    dependsOn jflex
}

apply {
    from rootProject.layout.projectDirectory.file('gradle/publish-config.gradle')
}
