apply plugin: 'com.gradle.plugin-publish'
apply plugin: 'groovy'
apply plugin: 'java-library'

group = 'cloud.wondrify'

ext {
	pomDescription = 'JVM Asset Pipeline Gradle Adapter.'
	pomMavenPublicationName = 'pluginMaven'
}

dependencies {

    implementation project(':asset-pipeline-core')

    // Gradle Plugin must be compiled with Groovy 3
    compileOnly "org.codehaus.groovy:groovy:$groovy3Version"
    compileOnly gradleApi()

    // These will be used by the Asset Pipeline Gradle Plugin for pre-compiling assets
    // and when running bootRun task (in development)
    runtimeOnly "com.google.javascript:closure-compiler-unshaded:$closureCompilerVersion"
    runtimeOnly "org.graalvm.js:js-community:$graalvmVersion"
}

task(console, dependsOn: 'classes', type: JavaExec) {
main = 'groovy.ui.Console'
classpath = sourceSets.main.runtimeClasspath
}

test {
	testLogging {
		exceptionFormat = 'full'
		showStandardStreams = true
	}
}

// The configuration example below shows the minimum required properties
// configured to publish your plugin to the plugin portal
pluginBundle {
	website = 'https://github.com/bertramdev/asset-pipeline-core/'
	vcsUrl = 'https://github.com/bertramdev/asset-pipeline-core/'
	description = 'JVM Asset Pipeline Gradle Adapter.'
	tags = ['assets', 'resources', 'asset-pipeline']

	plugins {
		assetPipelinePlugin {
			id = 'cloud.wondrify.asset-pipeline'
			displayName = 'Asset-Pipeline Plugin'
		}
	}
	mavenCoordinates {
		groupId = 'cloud.wondrify'
	}
}

apply {
	from rootProject.layout.projectDirectory.file('gradle/java-config.gradle')
	from rootProject.layout.projectDirectory.file('gradle/publish-config.gradle')
}