apply plugin: 'com.gradle.plugin-publish'
apply plugin: 'groovy'
apply plugin: 'java-library'

group = 'cloud.wondrify'

ext {
	pomDescription = 'JVM Asset Pipeline Gradle Adapter.'
	pomMavenPublicationName = 'pluginMaven'
}

dependencies {

    api project(':asset-pipeline-core')

    // Gradle Plugin must be compiled with Groovy 3
    compileOnly "org.codehaus.groovy:groovy:$groovy3Version"
    compileOnly gradleApi()

    // These will be used by the Asset Pipeline Gradle Plugin for pre-compiling assets
    // and when running bootRun task (in development)
    api "com.google.javascript:closure-compiler-unshaded:$closureCompilerVersion"
    api "org.graalvm.js:js-community:$graalvmVersion"
}

task(console, dependsOn: 'classes', type: JavaExec) {
	main = 'groovy.ui.Console'
	classpath = sourceSets.main.runtimeClasspath
}

test {
	testLogging {
		exceptionFormat = 'full'
		showStandardStreams = true
	}
}

// The configuration example below shows the minimum required properties
// configured to publish your plugin to the plugin portal
gradlePlugin {
	website = 'http://asset-pipeline.com/'
	vcsUrl = 'https://github.com/wondrify/asset-pipeline/'
	description = 'JVM Asset Pipeline Gradle Adapter.'

	plugins {
		assetPipelinePlugin {
			id = 'cloud.wondrify.asset-pipeline'
			displayName = 'Asset-Pipeline Plugin'
			description = findProperty('pomDescription')
			implementationClass = 'asset.pipeline.gradle.AssetPipelinePlugin'
			tags.set(['assets', 'resources', 'asset-pipeline'])
		}
	}
}

apply {
	from rootProject.layout.projectDirectory.file('gradle/java-config.gradle')
	from rootProject.layout.projectDirectory.file('gradle/publish-config.gradle')
}
