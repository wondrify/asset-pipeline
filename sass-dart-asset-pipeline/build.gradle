buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath "com.github.node-gradle:gradle-node-plugin:3.1.1"
    }
}

apply plugin: 'groovy'
apply plugin: 'java-library'

group = 'cloud.wondrify'

sourceCompatibility = '1.11'
targetCompatibility = '1.11'

ext {
    javetVersion = "1.1.0"
    pomDescription = 'Provides SASS Compiling using jsass (libsass) support for the Asset-Pipeline library.'
    pomDevelopers = ['gferon': 'Gabriel FÃ©ron']
}

repositories {
	mavenLocal()
    mavenCentral()
}

configurations {
    provided
}
 
sourceSets {
    main {
        compileClasspath += configurations.provided
    }
    test {
        compileClasspath += configurations.provided
    }
}

java {
    withSourcesJar()
    withJavadocJar()
}

apply plugin: 'com.github.node-gradle.node'

dependencies {
    implementation 'org.codehaus.groovy:groovy-all:3.0.20'
    implementation "com.caoccao.javet:javet-core:${javetVersion}"
    api project(':asset-pipeline-core')
    api 'org.slf4j:slf4j-api:1.7.28'
    testImplementation 'org.codehaus.groovy:groovy-all:3.0.20'
    testImplementation 'org.spockframework:spock-core:2.3-groovy-3.0'

    testRuntimeOnly 'org.slf4j:slf4j-simple:1.7.21'
    testRuntimeOnly 'org.webjars:bootstrap:5.1.3'

}

task copyJavetVersion()  {
    doLast {
        new File("${projectDir}/src/main/resources", "javet-version.txt").text = javetVersion
    }
}

task bundleJSCompiler(type: NpmTask, dependsOn: ['npmInstall']) {
    doFirst {
        mkdir('src/main/resources/js')
    }
    inputs.files(fileTree('src/main/js'), file('webpack.config.js'))
    outputs.file('src/main/resources/js/compiler.js')
    args = ['run', 'bundle']
}

compileGroovy.dependsOn copyJavetVersion, bundleJSCompiler

task(console, dependsOn: 'classes', type: JavaExec) {
  main = 'groovy.ui.Console'
  classpath = sourceSets.main.runtimeClasspath
}

test {
    testLogging {
        exceptionFormat = 'full'
        showStandardStreams = true
    }
}

node {
    download = true
    version = "16.13.0"
}

apply {
    from rootProject.layout.projectDirectory.file('gradle/publish-config.gradle')
}
