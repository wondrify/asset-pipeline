/*
 * build.gradle
 *
 * Copyright (c) 2014-2016, Daniel Ellermann
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


buildscript {
    ext {
        grailsVersion = project.grailsVersion
    }
    repositories {
        mavenLocal()
        maven { url = 'https://repo.grails.org/grails/core' }
        maven { url = 'https://repository.apache.org/content/repositories/snapshots' }
    }
    dependencies {
        classpath "org.apache.grails.gradle:grails-gradle-plugins:$grailsVersion"
        classpath "com.bertramlabs.plugins:asset-pipeline-gradle:2.13.2"
    }
}

group 'com.bertramlabs.plugins'

apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'java-library'
apply plugin: 'asset-pipeline'
apply plugin: 'org.apache.grails.gradle.grails-plugin'
apply plugin: 'org.apache.grails.gradle.grails-gsp'
apply plugin: 'org.apache.grails.gradle.grails-docs'

ext {
    grailsVersion = project.grailsVersion
    gradleWrapperVersion = project.gradleWrapperVersion
}

java {
    withSourcesJar()
    withJavadocJar()
}

repositories {
    mavenLocal()
    maven { url 'https://repo.grails.org/grails/core' }
    maven { url = 'https://repository.apache.org/content/repositories/snapshots' }
}

dependencyManagement {
    imports {
        mavenBom "org.apache.grails:grails-bom:$grailsVersion"
    }
    applyMavenExclusions false
}

dependencies {
    api 'org.springframework.boot:spring-boot-autoconfigure'
    api 'org.springframework.boot:spring-boot-starter-actuator'
    api 'org.springframework.boot:spring-boot-starter-logging'
    api 'org.springframework.boot:spring-boot-starter-tomcat'
    api "org.apache.grails:grails-core:$grailsVersion"
    api "org.apache.grails:grails-dependencies:$grailsVersion"
    api "org.apache.grails.web:grails-web-boot:$grailsVersion"
    api project(':asset-pipeline-core')
    api project(':asset-pipeline-grails')
    // api "com.bertramlabs.plugins:asset-pipeline-gradle:2.13.2"
    // api 'com.bertramlabs.plugins:asset-pipeline-grails:2.13.2'
    // api 'com.bertramlabs.plugins:asset-pipeline-core:2.13.2'

    console 'org.apache.grails:grails-console'

    profile 'org.grails.profiles:web-plugin:3.1.3'
    testImplementation('org.apache.grails:grails-testing-support-web')
}
task wrapper(type: Wrapper) {
    gradleVersion = gradleWrapperVersion
}

groovydoc {
    docTitle = "I18n asset pipeline API v${version}"
    windowTitle = "I18n asset pipeline API v${version}"
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId 'i18n-asset-pipeline-grails'
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name 'i18n-asset-pipeline-grails'
                    description 'asset-pipeline plugin to use localized messages in JavaScript.'
                    url 'https://github.com/bertramdev/asset-pipeline-core'
                    scm {
                        url 'https://github.com/bertramdev/asset-pipeline-core'
                        connection 'scm:https://bertramdev@github.com/bertramdev/asset-pipeline-core.git'
                        developerConnection 'scm:git://github.com/bertramdev/asset-pipeline-core.git'
                    }
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/license/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'davydotcom'
                            name 'David Estes'
                            email 'davydotcom@gmail.com'
                        }
                    }
                }
            }
            from components.java
        }
    }
    repositories {
        maven {
            if( !isReleaseVersion ) {
                url = "http://nexus.bertramlabs.com/content/repositories/snapshots"
                if(project.hasProperty('labsNexusUser')) {
                    credentials {
                        username = labsNexusUser
                        password = labsNexusPassword
                    }
                }
            }
        }
    }
}



assets {
    packagePlugin = true
}